plugins {
    id 'fabric-loom' version '1.6-SNAPSHOT' apply(false)
    id 'net.minecraftforge.gradle' version '[6.0,6.2)' apply(false)
    id 'org.spongepowered.gradle.vanilla' version '0.2.1-SNAPSHOT' apply(false)
    id 'org.spongepowered.mixin' version '0.7-SNAPSHOT' apply(false)
}

subprojects {
    apply plugin: 'java'

    java.toolchain.languageVersion = JavaLanguageVersion.of(17)
    java.withSourcesJar()
    java.withJavadocJar()

    jar {
        from(rootProject.file("LICENSE.txt")) {
            rename { "LICENCE_${mod_id}.txt" }
        }
        manifest {
            attributes([
                'Specification-Title'     : mod_name,
                'Specification-Vendor'    : mod_author,
                'Specification-Version'   : project.jar.archiveVersion,
                'Implementation-Title'    : project.name,
                'Implementation-Version'  : project.jar.archiveVersion,
                'Implementation-Vendor'   : mod_author,
                'Implementation-Timestamp': new Date().format("yyyy-MM-dd'T'HH:mm:ssZ"),
                'Timestamp'               : System.currentTimeMillis(),
                'Built-On-Java'           : "${System.getProperty('java.vm.version')} (${System.getProperty('java.vm.vendor')})",
                'Built-On-Minecraft'      : minecraft_version
            ])
        }
    }

    sourcesJar {
        from(rootProject.file("LICENSE.txt")) {
            rename { "LICENCE_${mod_id}.txt" }
        }
    }

    repositories {
        mavenCentral()
        maven { url 'https://repo.spongepowered.org/repository/maven-public/' }
        maven { url 'https://maven.blamejared.com' }
        maven { url 'https://api.modrinth.com/maven' }
        maven { url 'https://raw.githubusercontent.com/Fuzss/modresources/main/maven/' }
    }

    tasks.withType(JavaCompile).configureEach {
        it.options.encoding = 'UTF-8'
        it.options.getRelease().set(17)
    }

    processResources {
        def expandProps = [
            version: version,
            group: project.group,

            mod_id: mod_id,
            mod_name: mod_name,
            mod_author: mod_author,
            mod_url: mod_url,
            description: project.description,
            license: license,

            minecraft_version: minecraft_version,
            minecraft_version_range: minecraft_version_range,
            fabric_version: fabric_version,
            fabric_loader_version: fabric_loader_version,
            forge_version: forge_version,
            forge_version_range: forge_version_range,
            forge_loader_version_range: forge_loader_version_range,

            forgeconfigapiport_version: forgeconfigapiport_version,
            jade_version_fabric: jade_version_fabric,
            jade_version_forge: jade_version_forge,
            jade_version_range: jade_version_range,
        ]

        def inFiles = [
            "pack.mcmeta",
            "fabric.mod.json",
            "META-INF/mods.toml",
            "*.mixins.json",
        ]

        filesMatching(inFiles) { expand expandProps }
        inputs.properties(expandProps)
    }
}
